plugins {
  id 'org.springframework.boot' version '3.3.1'
  id 'io.spring.dependency-management' version '1.1.5'
  id 'java'
  id 'jacoco'
  id 'checkstyle'
  id 'war'
  id 'org.sonarqube' version '5.0.0.4638'
  id 'org.owasp.dependencycheck' version '6.1.2'
}

group = 'uk.co.fivium'
version = System.getenv("DRONE_BRANCH") ? System.getenv("DRONE_BRANCH").replaceAll("[/\\-]", "_") : "LOCAL-SNAPSHOT"
sourceCompatibility = '21'

tasks {
  bootJar {
    archiveFileName.set("${rootProject.name}.jar")
  }
}

repositories {
  mavenCentral()
  maven { url "https://nexus.fivium.co.uk/repository/maven-releases/" }
  maven { url "https://nexus.fivium.co.uk/repository/maven-snapshots/" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {

  providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

  implementation 'org.springframework.boot:spring-boot-starter'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-jdbc'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework:spring-test'
  implementation 'org.springframework.boot:spring-boot-starter-freemarker'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'io.micrometer:micrometer-registry-statsd'

  implementation 'org.springframework.boot:spring-boot-starter-quartz'

  // Hibernate 6.5 introduced a performance issue with certain Oracle queries, see https://hibernate.atlassian.net/browse/HHH-18352.
  // This was fixed in a 6.5.3 snapshot but development moved over to 6.6.0 before a final 6.5.3 was released.
  // The upcoming spring-data-jpa 3.4.x will use Hibernate 6.6 https://github.com/spring-projects/spring-data-jpa/issues/3574, until then we
  // use a pinned 6.5.3 snapshot which has passed all the Hibernate CI tests, and which we have tested internally.
  implementation 'org.hibernate.orm:hibernate-core:6.5.3-20240807.164253-67'

  implementation 'org.flywaydb:flyway-core:9.5.1-FIVIUM'
  implementation 'com.oracle.database.jdbc:ojdbc11:21.9.0.0'
  implementation 'uk.gov.service.notify:notifications-java-client:5.1.0-RELEASE'
  implementation 'org.springframework.session:spring-session-jdbc'
  implementation('org.camunda.bpm.springboot:camunda-bpm-spring-boot-starter:7.21.0') {
    // Exclude com.sun.xml.bind JAXB as it conflicts with glassfish JAXB from Hibernate Envers. The packages inside
    // the dependencies are the same so it is fine to exclude it.
    exclude group: 'com.sun.xml.bind', module: 'jaxb-impl'
  }

  implementation 'org.springframework.data:spring-data-envers'

  // automatically generates jpa meta-model for type safe criteria builder queries
  annotationProcessor("jakarta.xml.bind:jakarta.xml.bind-api")
  annotationProcessor("org.hibernate.orm:hibernate-jpamodelgen")

  implementation 'com.google.guava:guava:33.2.1-jre'
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'org.apache.commons:commons-collections4:4.4'
  implementation('commons-validator:commons-validator:1.7') {
    // Exclude commons-logging to fix "Standard Commons Logging discovery in action with spring-jcl: please remove
    // commons-logging.jar from classpath in order to avoid potential conflicts" warning on startup.
    exclude group: 'commons-logging', module: 'commons-logging'
  }

  implementation group: 'fi.solita.clamav', name: 'clamav-client', version: '1.0.1'

  // https://mvnrepository.com/artifact/com.openhtmltopdf/openhtmltopdf-core
  implementation group: 'com.openhtmltopdf', name: 'openhtmltopdf-core', version: '1.0.10'
  // https://mvnrepository.com/artifact/com.openhtmltopdf/openhtmltopdf-pdfbox
  implementation group: 'com.openhtmltopdf', name: 'openhtmltopdf-pdfbox', version: '1.0.10'

  // https://mvnrepository.com/artifact/org.jsoup/jsoup
  implementation group: 'org.jsoup', name: 'jsoup', version: '1.16.1'

  // https://mvnrepository.com/artifact/org.imgscalr/imgscalr-lib
  implementation group: 'org.imgscalr', name: 'imgscalr-lib', version: '4.2'

  implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.15.2'
  implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.15.2'

  implementation('uk.co.fivium.feedbackmanagementservice:client:2.1.1')

  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

  testImplementation 'com.h2database:h2:1.4.200'
  testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.16.1'
  testImplementation(
      'org.springframework.boot:spring-boot-starter-test',
      'org.springframework.security:spring-security-test',
      'com.squareup.okhttp3:mockwebserver:4.9.1',
      'com.squareup.okhttp3:okhttp:4.9.1' // Required otherwise MockWebServer gives ClassNotFoundException
  )
  testImplementation 'junit:junit:4.13.1'

}

checkstyle {
  config = rootProject.resources.text.fromFile('ide/checkstyle.xml')
  toolVersion '8.28'
  ignoreFailures = false
  maxWarnings = 0
  // Sets suppressionFile property so checkstyle during build ignores generated files.
  // The default path used by ide is not relative so will cause failures if that is used in isolation.
  configProperties = [ "suppressionFile" : project(':').file('ide/checkstyle-suppression.xml')]
}

dependencyCheck {
  data {
    // Point to a persisted volume in drone, to avoid having to re-download the whole CVE database each run.
    // Comment this out to run locally
    directory='/var/nvd-db'
  }
}

test {
  minHeapSize = "5g" // initial heap size
  maxHeapSize = "10g" // maximum heap size
}

tasks.jacocoTestReport {
  reports {
    xml.required = true
  }
}

sonarqube {
  properties {
    property("sonar.projectKey", "fiviumuk_pipeline-works-authorisations")
    property("sonar.host.url", "https://sonarcloud.io")
    property("sonar.login", System.getenv("SONARCLOUD_TOKEN"))
    property("sonar.organization", "fivium")
    property("sonar.branch.name", System.getenv("DRONE_BRANCH"))
    if (System.getenv("DRONE_BRANCH").startsWith("feature")) {
      property("sonar.branch.target", "develop")
    }
    property("sonar.exclusions", "src/test/**, src/main/resources/public/assets/**, fivium-design-system-core/**, src/main/resources/templates/fds/**, build/**")
    property("sonar.sources", "src")
  }
}
